// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

import "../../../lib/forge-std/src/Test.sol";
import "../../../lib/forge-std/src/console.sol";
import "./CelestiaL1DAValidator.sol";
import "../../IL1DAValidator.sol";
import {ISP1Verifier} from "./ISP1Verifier.sol";

contract CelestiaL1DAValidatorTest is Test {
    CelestiaL1DAValidator public validator;

    // Test variables
    uint256 public constant MAX_BLOBS_SUPPORTED = 1;

    // Fork-related variables
    uint256 public mainnetFork;
    string public MAINNET_RPC_URL;
    bool public forksInitialized;

    function setUp() public {
        // Get the RPC URL from environment variable
        try vm.envString("MAINNET_RPC_URL") returns (string memory url) {
            MAINNET_RPC_URL = url;
            console.log("Using MAINNET_RPC_URL:", MAINNET_RPC_URL);

            // Create a fork of mainnet
            mainnetFork = vm.createFork(MAINNET_RPC_URL);
            vm.selectFork(mainnetFork);
            forksInitialized = true;

            console.log("Testing against mainnet fork at block:", block.number);
        } catch {
            console.log("MAINNET_RPC_URL not set. Mock testing only.");
            forksInitialized = false;
        }

        // Deploy the validator contract
        validator = new CelestiaL1DAValidator(
            0x397A5f7f3dBd538f23DE225B51f532c34448dA9B, // SP1Groth16Verifier, Sepolia deployment
            0xF0c6429ebAB2e7DC6e05DaFB61128bE21f13cb1e, // Blobstream, Sepolia deployment
            0x009e55c6e4a99bfea454bb3575dc8558470f4d3347744821f1022fe8504da65a // EQS Vkey
        );
    }

    function testMultipleCases() public {
        // Skip test if fork is not available
        if (!forksInitialized) {
            console.log("Skipping test - MAINNET_RPC_URL not set");
            return;
        }

        // Read and parse the JSON file
        string memory json = vm.readFile("celestia_test_cases.json");
        string[] memory testCases = abi.decode(vm.parseJson(json), (string[]));

        console.log("Running", testCases.length, "test cases");

        // Make sure we're using the fork
        vm.selectFork(mainnetFork);

        // Run each test case
        for (uint i = 0; i < testCases.length; i++) {
            console.log("\nTesting case", i);
            
            // Convert hex string to bytes
            // first 32 bytes of real-world operator DA input contains state diff hash
            bytes memory testCase = vm.parseBytes(testCases[i]);

            // first 32 bytes of real-world operator DA input contains state diff hash
            // new bytes(32) will
            bytes memory operatorDAInput = bytes.concat(new bytes(32), testCase);
            console.log("Parsed input");

            // Decode the input
            CelestiaZKStackInput memory decodedInput = abi.decode(testCase, (CelestiaZKStackInput));
            console.log("Decoded input");

            // In the real world, the "eqKeccakHash" comes from the L2 DA contract
            // we know it's valid because it's proved in the zkEVM.
            (bytes32 eqKeccakHash, bytes32 eqDataRoot) = abi.decode(decodedInput.publicValues, (bytes32, bytes32));

            try validator.checkDA(0, 0, keccak256(abi.encodePacked(new bytes(32), eqKeccakHash)), operatorDAInput, MAX_BLOBS_SUPPORTED) returns (L1DAValidatorOutput memory output) {
                console.log("Case", i, "passed!");
                console.log("State diff hash:", vm.toString(output.stateDiffHash));
            } catch Error(string memory reason) {
                console.log("Case", i, "failed with reason:", reason);
                fail();
            } catch (bytes memory lowLevelData) {
                console.log("Case", i, "failed with low level error");
                fail();
            }
        }
    }
}
